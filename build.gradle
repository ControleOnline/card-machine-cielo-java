plugins {
    id 'com.android.library' version '8.1.0'
    id 'maven-publish'
    id 'signing'
}


def androidPropertiesFile = rootProject.file("./config/android/env.properties")
def env = new Properties()
if (androidPropertiesFile.exists()) {
    env.load(new FileInputStream(androidPropertiesFile))
} else {
    throw new GradleException("Arquivo keystore.properties não encontrado em: ${androidPropertiesFile.absolutePath}")
}


android {
    compileSdk 34
    namespace 'com.controleonline.pos.cielo'
    defaultConfig {
        minSdk 24
        targetSdk 34
        versionCode 1
        versionName "1.0.0"
    }
    signingConfigs {
        release {
            storeFile file(env["MYAPP_UPLOAD_STORE_FILE"])
            storePassword env["MYAPP_UPLOAD_STORE_PASSWORD"]
            keyAlias  env["MYAPP_UPLOAD_KEY_ALIAS"]
            keyPassword env["MYAPP_UPLOAD_KEY_PASSWORD"]
        }
        debug {
            storeFile file('debug.keystore')
            storePassword 'android'
            keyAlias 'androiddebugkey'
            keyPassword 'android'
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
}

dependencies {
    implementation 'androidx.annotation:annotation:1.7.0'
    implementation("com.facebook.react:react-android:0.72.4")
}

group = 'com.github.ControleOnline'
version = '1.0.0'

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/ControleOnline/card-machine-cielo-java")
            credentials {
                username = System.getenv("GITHUB_ACTOR")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
    }
    publications {
        release(MavenPublication) {
            groupId = project.group
            artifactId = 'cielo'
            version = project.version
            afterEvaluate {
                artifact("$buildDir/outputs/aar/${project.name}-release.aar")
            }
            pom {
                packaging = 'aar'
                name.set('Cielo POS Library for Android')
                description.set('Uma biblioteca Android para integração com dispositivos Cielo POS.')
                url.set('https://github.com/ControleOnline/card-machine-cielo-java')
                licenses {
                    license {
                        name.set('The Apache License, Version 2.0')
                        url.set('http://www.apache.org/licenses/LICENSE-2.0.txt')
                    }
                }
                developers {
                    developer {
                        id.set('ControleOnline')
                        name.set('ControleOnline')
                        email.set('suporte@controleonline.com.br')
                    }
                }
                scm {
                    connection.set('scm:git:git://github.com/ControleOnline/card-machine-cielo-java.git')
                    developerConnection.set('scm:git:ssh://github.com:ControleOnline/card-machine-cielo-java.git')
                    url.set('https://github.com/ControleOnline/card-machine-cielo-java.git')
                }
            }
        }
    }
}

signing {
    sign publishing.publications.release
}

// Adicione esta linha para definir explicitamente a dependência
tasks.named('signReleasePublication').configure {
    dependsOn 'bundleReleaseAar'
}